/*----Version 2.9 (HO TPTP BNF2 version.internal development number) */

/*----DRAFT VERSION FOR EVALUATION OF HOF FEATURES.  Not in sync with TPTP. */

/*----Version 3.1.1.13b (TPTP version.internal development number) */
/*----Intended uses of the various parts of the TPTP syntax are explained */
/*----in the TPTP technical manual, linked from www.tptp.org. */
/*----See the <comment> entries regarding text that can be discarded before  */
/*----tokenizing for this syntax. */
/*----White space is almost irrelevant, but all operators consisting of */
/*----non-alphanumeric characters should be followed by a space. */
/*----This simplifies unambiguous parsing especially for the Prolog people. */

/*----This document uses BNF2, a meta-language close to the original BNF that */
/*----was used to define Algol 60 [Naur et al, 1960], rather than EBNF. */
/*----Grammar symbols are enclosed in < > and most characters not so */
/*----enclosed stand for themselves. */
/*----BNF2 uses some extensions to reflect the modern practice of separating */
/*----the processing of production rules (context-free grammar, normally) */
/*----from the recognition of tokens (regular expressions, normally), */
/*----hence the suffix 2. */
/*----This separation aids automated translation, using tools such as */
/*----lex, yacc, flex, bison, and others. */
/*----The extensions are signalled by use of one of these nonstandard */
/*----meta-symbols (where a standard rulewould have ::=): :== or ::- or ::: . */

/*----For example, when <real> appears in a grammar rule, it is a grammar */
/*----symbol, and since it is not on the left side of any rule, it is */
/*----a terminal symbol (token).  The DEFINITION of <real> is given */
/*----by a lexical rule beginning <real> ::- ..., to distinguish it from a */
/*----BNF grammar rule, such as <number> ::= ... . */
/*----In an automated translation, the former occurrence would become a */
/*----yacc/bison rule, and that latter occurrence would become a lex/flex rule. */
/*----Macros may be introduced to aid in specifying tokens by using ::: . */
/*----In this example, <digit> is a macro, not a token: */
/*----<digit>  ::: [0-9] */

/*----A different meta-symbol, :== , is used to define a strict language */
/*----that excludes input sentences that have the correct form but incorrect */
/*----contents. */
/*----The design guideline is that a syntax error occurs when the parser */
/*----does not know what the user is TRYING to say, */
/*----while a semantic error occurs when the parser knows what the user */
/*----is trying to say, but there is something wrong with it. */
/*----The goal is to define a relaxed syntactic language that is amenable */
/*----to automated parsing, then refine it to a strict language by means */
/*----of semantic actions, which are typically hand-coded. */
/*----Of course, various implementers will make varying choices of how much */
/*----to automate parser generation, and how much to hand-code. */
/*----Some semantic actions are specified using :==. */

/*----Enumerated types are a simple example.  A relaxed rule and a strict */
/*----rule may be given: */
/*----<nonterminal> ::= <expr> */
/*----<nonterminal> :== val1 | val2 | val3 ... . */
/*----val1, val2, val3, ... are specific character sequences (strings) that are */
/*----in the more general class <expr>, and they specify exactly which values */
/*----are permitted for <nonterminal>.  The syntactic part of the parser */
/*----may accept any instance of <expr> and leave the detailed checking of */
/*----values to a semantic action. */
/*----The lexical analyzer need not recognize specific values within <expr> */
/*----as different tokens.  (The specific values should be attached to */
/*----tokens as "lexical values" for later processing.) */

/*----A more general form is */
/*----<nonterminal> ::= <expr> */
/*----<nonterminal> :== <expr_one> | <expr_two> ... */
/*----The meaning is the same as in the first form, except that <expr_one>, */
/*----<expr_two>, ... may be nonterminals as well as specific strings. */

/*----A nonterminal that occurs ONLY in the strict language is defined */
/*----with :== and has no ::= rule at all: */
/*----<expr_one> :== <expr_three> ... */
/*----This declares that <expr_one> is not needed for the */
/*----relaxed syntactic language and this rule only describes part of the */
/*----strict language. */

/*----A line in this document that begins "% $$" provides additional */
/*----syntactic or semantic information. */
/*----Semantic information supplements the syntactic information in */
/*----the BNF grammar rule. */
/*----Syntactic information informally describes a construct that */
/*----is too complicated to describe formally in the BNF2 framework, */
/*----or a construct whose formal description might be possible, but would */
/*----be so complicated that it would be difficult to work with. */
/*----Escapes sequences in token definitions are a typical example. */

/*-------------------------------------------------------------------------- */
/*----Files. Empty file is OK. */
<TPTP_file>          ::= <TPTP_input>*
<TPTP_input>         ::= <annotated_formula> | <include> |
                         <comment> | <system_comment>

/*----Formula records */
<annotated_formula>  ::= <hof_annotated> | <cnf_annotated> |
                         <TPTP_input_formula> | <TPTP_input_clause>
/*----Future languages may include ...  english | efof | tfof | mathml | ... */
/*----Keyword fof means the same as hof, retained for backward compatibility. */
<hof_annotated>      ::= hof(<name>,<formula_role>,<hof_formula><annotations>). |
                         hof(<name>,<formula_role>,<hof_definition><annotations>). |
                         fof(<name>,<formula_role>,<hof_formula><annotations>).
<cnf_annotated>      ::= cnf(<name>,<formula_role>,<cnf_formula><annotations>).
<annotations>        ::= <null> | ,<source> | ,<source>,<useful_info>

/*----Types for problems. */
/*----Note: The previous <source_type> from ... */
/*----   <formula_role> ::= <user_role>-<source>  */
/*----... is now gone. Parsers may choose to be tolerant of it for backwards  */
/*----compatibility. */
<formula_role>       ::= <atomic_word>
<formula_role>       :== axiom |
                         hypothesis |
                         definition |
                         lemma |
                         theorem |
                         conjecture |
                         lemma_conjecture |
                         negated_conjecture |
                         plain |
                         unknown
/*----"axiom"s are accepted, without proof, as a basis for proving "conjecture"s  */
/*----and "lemma_conjecture"s in FOF problems. In CNF problems "axiom"s are  */
/*----accepted as part of the set whose satisfiability has to be established. */
/*----There is no guarantee that the axioms of a problem are consistent. */
/*----"hypothesis"s are assumed to be true for a particular problem, and are  */
/*----used like "axiom"s. */
/*----"definition"s are used to define symbols, and are used like "axiom"s. */
/*----"lemma"s and "theorem"s have been proven from the "axiom"s, can be used  */
/*----like "axiom"s, but are redundant wrt the "axiom"s. "lemma" is used as the  */
/*----role of proven "lemma_conjecture"s, and "theorem" is used as the role of  */
/*----proven "conjecture"s, in output. A problem containing a "lemma" or  */
/*----"theorem" that is not redundant wrt the "axiom"s is ill-formed. "theorem"s */
/*----are more important than "lemma"s from the user perspective. */
/*----"conjecture"s occur in only FOF problems, and are to all be proven from  */
/*----the "axiom"(-like) formulae. A problem is solved only when all  */
/*----"conjecture"s are proven. */
/*----"lemma_conjecture"s are expected to be provable, and may be useful to  */
/*----prove, while proving "conjecture"s. */
/*----"negated_conjecture"s ocuur in only CNF problems, and are formed from */
/*----negation of a "conjecture" in a FOF to CNF conversion. */
/*----"plain"s have no special user semantics, and can be used like "axiom"s. */
/*----"unknown"s have unknown role, and this is an error situation. */

/*----HOF formulae. All formulae must be closed. */
<hof_formula>        ::= <binary_formula> | <unitary_formula>

/*----HOF definition must be at top level. := is NOT included in <tptp_operator> */
/*----The construct is EXPERIMENTAL and the syntax is flexible to give */
/*----users plenty of rope.  Future versions might cut back or expand. */
/*----Examples: */
/*----const := <expr_not_containing_const>  Is pretty self-explanatory. */
/*----(const : A) := <expr_not_containing_const but containing A>   Seems OK?? */
/*----const := <expr_containing_const_recursively>   Be consistent with coq?? */
/*----functor(<T1>,<T2>) := <expr_containing_<T1> and <T2>>   Skating on */
/*----thin ice??  <T1> and <T2> are fof terms.  Maybe OK if right side is fof? */
/*----Users are encouraged to supply <system_comment>s to explain how they */
/*----intend any format other than the first one to be interpreted. */
<hof_definition>     ::= <defined_element> := <unitary_formula>
<defined_element>    ::= <plain_term> | <typed_constant>

<tptp_operator>      ::= <unary_connective> | <binary_connective> |
                         <assoc_connective> | <equality_connective>
<binary_formula>     ::= <nonassoc_binary> | <assoc_binary> | <apply_term>
/*----Only some binary connectives are associative */
/*----There's no precedence amoung binary connectives */
<nonassoc_binary>    ::= <unitary_formula> <binary_connective> <unitary_formula>
<binary_connective>  ::= <=> | => | <= | <~> | ~<vline> | ~&
/*----Associative connectives & and | are in <assoc_binary> */
<assoc_binary>       ::= <or_formula> | <and_formula>
<or_formula>         ::= <unitary_formula> <vline> <unitary_formula>
                         <more_or_formula>*
<more_or_formula>    ::= <vline> <unitary_formula>
<and_formula>        ::= <unitary_formula> & <unitary_formula>
                         <more_and_formula>*
<more_and_formula>   ::= & <unitary_formula>
<assoc_connective>   ::= & | <vline>
/*----<unitary_formula> are in ()s or do not have a <binary_connective> at the  */
/*----top level. */
<unitary_formula>    ::= <quantified_formula> | <unary_formula> |
                         <lambda_term> | <lower_formula>
/* | (<hof_formula>) */

<quantified_formula> ::= <quantifier> [<variable_decls>] : <unitary_formula>
<quantifier>         ::= ! | ?
/*----! is universal quantification and ? is existential. Syntactically, the  */
/*----quantification is the left operand of :, and the <unitary_formula> is  */
/*----the right operand. Although : is a binary operator syntactically, it is  */
/*----not a <binary_connective>, and thus a <quantified_formula> is a  */
/*----<unitary_formula>. */
/*----Universal   example: ! [X,Y] : ((p(X) & p(Y)) => q(X,Y)). */
/*----Existential example: ? [X,Y] : (p(X) & p(Y)) & ~ q(X,Y). */
/*----Quantifiers have higher precedence than binary connectives, so in */
/*----the existential example the quantifier applies to only (p(X) & p(Y)). */


<lower_formula>      ::= <hof_atom> | (<hof_formula>)

/*----<hof_atom> is <atomic_formula> in the context of <hof_formula>. */
/*----<atomic_formula> is retained for integrity of <cnf_formula>. */
<hof_atom>           ::= <term> | <defined_hof_atom> | <typed_constant>
<defined_hof_atom>   ::= $true | $false |
                         <lower_formula> = <type_term> |
                         <lower_formula> = <lower_formula> |
                         <lower_formula> != <lower_formula> 



/*----<apply_term> must have @ at top level. */
/*----<lambda_term> can drop down to <term> immediately. */
/*----<term> cannot climb back up to <apply_term>, or <lambda_term>. */
/*----In this context &, |, ~, $true, $false are "macros" for */
/*----boolean functions that could be denoted with lambda terms. */
/*----E.g., & : o->o->o. */

<apply_term>         ::= <apply_term> @ <unitary_formula> |
                         <apply_term> @ <type_term> |
                         <unitary_formula> @ <unitary_formula> |
                         <unitary_formula> @ <type_term>
<lambda_term>        ::= <lambda_symbol> [<variable_decls>] : <unitary_formula>

/*----@ (denoting apply) is left-associative and lambda is right-associative. */
/*----lambda [X] : lambda [Y] : f @ g (where f and g are <apply_terms>s) */
/*----should be parsed as (lambda [X] : (lambda [Y] : f)) @ g. */
/*----Its Beta reduction is lambda [Y] : f[X:=g], a function of Y, */
/*----in accordance with the usual Currying. */
/*----(Notation follows Stansifer; [Benzmuller, Brown 2005] writes [g/X]f.) */
/*----If X occurs in g, it is outside the scope of the lambda [X] shown */
/*----and the Beta reduction needs to be preceded by a rename of the bound X. */
/*----Of course it is safer and clearer not to overwork bound variables. */
/*----Use parenthese to get lambda [X] : (lambda [Y] : f @ g), or */
/*----lambda [X] : (lambda [Y] : (f @ g)). */

/*----lambda might have a symbol in the future. */
<lambda_symbol>      ::= lambda | ^

<variable_decls>     ::= <variable_decl> | <variable_decl>,<variable_decls>
/*----Future variables may have sorts and existential counting */
/*----<variable_decl> is untyped or typed variable declaration. */
<variable_decl>      ::= <variable> | <typed_variable>
<typed_variable>     ::= <variable> : <type_expr>
<typed_constant>     ::= (<constant> : <type_expr>)

/*----<type_term> is disjoint from <term>, but some <term>s can be types. */
/*----<map_arrow> is right-associative: o->o->o means o->(o->o). */
<type_term>          ::= (<unitary_type> <map_arrow> <type_expr>) |
                         <defined_type>
<type_expr>          ::= <unitary_type> <map_arrow> <type_expr> |
                         <unitary_type>
<unitary_type>       ::= <atomic_type> |
                         (<unitary_type> <map_arrow> <type_expr>)
<atomic_type>        ::= <term> | <defined_type>
<defined_type>       ::= $o | $i | $type
<map_arrow>          ::= -> | >



/*----Unary connectives bind more tightly than binary */
<unary_formula>      ::= <unary_connective> <unitary_formula>
<unary_connective>   ::= ~

/*----CNF formulae (variables implicitly universally quantified) */
<cnf_formula>        ::= (<disjunction>) | <disjunction>
<disjunction>        ::= <literal> <more_disjunction>*
<more_disjunction>   ::= <vline> <literal>
<literal>            ::= <atomic_formula> | ~ <atomic_formula>

/*----Atoms (<predicate> is not used currently) */
<atomic_formula>     ::= <plain_atom> | <defined_atom> | <system_atom>
<plain_atom>         ::= <plain_term>
/*----A <plain_atom> looks like a <plain_term>, but really we mean */
/*----  <plain_atom>         ::= <proposition> | <predicate>(<arguments>) */
/*----  <proposition>        ::= <atomic_word> */
/*----  <predicate>          ::= <atomic_word> */
/*----Using <plain_term> removes a reduce/reduce ambiguity in lex/yacc. */
<arguments>          ::= <term> | <term>,<arguments>
<defined_atom>       ::= $true | $false | <term> <equality_connective> <term>
<equality_connective>  ::= = | !=
/*----Some systems still interprete equal/2 as equality. The use of equal/2 */
/*----for other purposes is therefore discouraged. Please refrain from either  */
/*----use. Use infix '=' for equality. Note: <term> != <term> is equivalent */
/*----to ~ <term> = <term>.  Use of != is DEPRECATED and may be dropped soon. */
/*----More defined atoms may be added in the future. */
<system_atom>        ::= <system_term>
/*----<system_atom>s are used for evaluable predicates that are available */
/*----in particular tools. The predicate names are not controlled by the */
/*----TPTP syntax, so use with due care. The same is true for <system_term>s. */

/*----Terms */
<term>               ::= <function_term> | <variable>
<function_term>      ::= <plain_term> | <defined_term> | <system_term>
<plain_term>         ::= <constant> | <functor>(<arguments>)
<constant>           ::= <atomic_word> | (<tptp_operator>)
<functor>            ::= <atomic_word>
/*----<numbers>s and <distinct_object>s are always interpreted as themselves */
<defined_term>       ::= <number> | <distinct_object>
<system_term>        ::= <system_constant> | <system_functor>(<arguments>)
<system_functor>     ::= <atomic_system_word>
<system_constant>    ::= <atomic_system_word>
<variable>           ::= <upper_word>

/*----Formula sources */
<source>             ::= <general_function>
<source>             :== <dag_source> | <internal_source> | <external_source> |
                         unknown
/*----Only a <dag_source> can be a <name>, i.e., derived formulae can be */
/*----identified by a <name> or an <inference_record>  */
<dag_source>         :== <name> | <inference_record>
<inference_record>   :== inference(<inference_rule>,<useful_info>,
                         [<parent_info_list>])
<inference_rule>     :== <atomic_word>
/*----Examples are        deduction | modus_tollens | modus_ponens | rewrite |  */
/*                        resolution | paramodulation | factorization |  */
/*                        cnf_conversion | cnf_refutation | ... */
<parent_info_list>   :== <parent_info> | <parent_info>,<parent_info_list>
<parent_info>        :== <source><parent_details>
<theory>             :== theory(<theory_name>)
<theory_name>        :== equality | ac
/*----More theory names may be added in the future. */
<parent_details>     :== :<atomic_word> | <null>
<internal_source>    :== introduced(<intro_type><intro_info>)
<intro_type>         :== definition | axiom_of_choice | tautology
/*----This should be used to record the symbol being defined, or the function */
/*----for the axiom of choice */
<intro_info>         :== ,<useful_info> | <null>
<external_source>    :== <file_source> | <creator_source> | <theory>
<file_source>        :== file(<file_name>,<file_node_name>)
<file_node_name>     :== <name> | unknown
<creator_source>     :== creator(<creator_name><creator_info>)
<creator_name>       :== <atomic_word>
<creator_info>       :== ,<useful_info> | <null>

/*----File name and Useful Info fields */
<file_name>          ::= <atomic_word>
<useful_info>        ::= <general_list>
<useful_info>        :== [] | [<info_items>]
<info_items>         :== <info_item> | <info_item>,<info_items>
<info_item>          :== <formula_item> | <inference_item> | <general_function>
/*----Useful info for formula records */
<formula_item>       :== <description_item> | <iquote_item> 
<description_item>   :== description(<atomic_word>)
<iquote_item>        :== iquote(<atomic_word>)
/*----Useful info for inference records */
<inference_item>     :== <inference_status> | <refutation>
<inference_status>   :== status(<status_value>)
/*----These are the status values from the SZS ontology */
<status_value>       :== tau | tac | eqv | thm | sat | cax | noc | csa | cth |
                         ceq | unc | uns | sab | sam | sar | sap | csp | csr |
                         csm | csb
<inference_info>     :== <inference_rule>(<atomic_word>,<general_list>)
<refutation>         :== refutation(<file_source>)
/*----Useful info for creators is just <general_function> */

/*----Include directives */
<include>            ::= include(<file_name><formula_selection>).
<formula_selection>  ::= <null> | ,[<name_list>]
<name_list>          ::= <name> | <name>,<name_list>

/*----General purpose */
<general_term>       ::= <general_function> | <general_list>
<general_list>       ::= [] | [<general_arguments>]
<general_function>   ::= <constant> | <functor>(<general_arguments>)
<general_arguments>  ::= <general_term> | <general_term>,<general_arguments>

<name>               ::= <atomic_word> | <unsigned_integer>
<atomic_word>        ::= <lower_word> | <single_quoted>
<null>               ::=

/*----Numbers with different values are implicitly unequal; i.e. ~(3.0 = 3). */
<number>             ::= <real> | <integer>
<integer>            ::= <signed_integer> | <unsigned_integer>

/*------------------------------------------------------------------------------ */
/*----Rules from here on down are for defining tokens (terminal symbols) */
/*----of the grammar, assuming they will be recognized by a lexical scanner. */

/*----Usual regexp notation is used mostly, as in grep, lex, awk, sed, etc. */
/*----[ ] denotes a selection of ONE of the enclosed characters; e.g. */
/*----[0-9] means one digit.  Inside [ ], special meanings are disabled; */
/*----e.g., [.] is a period whereas . is any character except a newline. */
/*----[^abc] means any character EXCEPT a, b, or c.  [\^] matches a caret. */
/*----[\\] matches a backslash.  See lex/flex documentation for details. */
/*----For uniformity and safety, [ ] is placed around single characters even */
/*----when not necessary. */

/*----A ::- rule defines a token. */
/*----A ::: rule defines a macro that is not a token. */
/*----"..." means the definition is imprecise and manual coding is needed. */
/*----"% $$" provides semantic information, and explains "...". */
<digit>                ::: [0-9]
<lower>                ::: [a-z]
<upper>                ::: [A-Z]
<alphanum>             ::: (<lower>|<upper>|<digit>|[_])
<any_char>             ::: (.|\n)
<ddollar>              ::: [$][$]

<vline>                ::- [|] ...
/* $$ token for <vline> is VLINE, conflicts with meta-symbol, hard-coded. */

<real>                 ::- (<signed_integer>|<unsigned_integer>)<decimal_part>
<signed_integer>       ::- [+-]<unsigned_integer>
<unsigned_integer>     ::- <digit><digit>*
<decimal_part>         ::- [.]<digit><digit>*

<upper_word>           ::- <upper><alphanum>*
<lower_word>           ::- <lower><alphanum>*
<atomic_system_word>   ::- <ddollar><lower><alphanum>*

/*----System Comments are used for system specific annotation of anything. */
/*----They look like comments (see next), so by default they are discarded. */
/*----However, a wily user of the syntax can notice the $$ and store/extract */
/*----information from the "comment". */
/*----System specific annotations should begin $$, then identify the system, */
/*----followed by a :, e.g., /* $$ Otter 3.3: Demodulator */

/*----Comments. These may be retained for non-logical purposes. Comments */
/*----can occur only between annotated formulae (see <TPTP_input>), but */
/*----it seems likely that people will put them elsewhere and simply */
/*----strip them before tokenising. */

/* $$ A string that matches both <system_comment> and <comment> should be */
/* $$ recognized as <system_comment>. */
<system_comment>       ::- <system_comment_one>|<system_comment_multi>
<comment>              ::- <comment_one>|<comment_multi>
<system_comment_one>   ::: [%][ ]*<ddollar>.*
<system_comment_multi> ::: [/][*][ ]*<ddollar>([^*]*[*][*]*[^/*])*[^*]*[*][*]*[/]
/* $$ Approximately, [/][*][ ]*<ddollar><any_char>*[*][/] */
/* $$ Correct regexp ends at FIRST [*][/] */
/* $$ See comment for <comment_multi> below. */
<comment_one>          ::: [%].*
<comment_multi>        ::: [/][*]([^*]*[*][*]*[^/*])*[^*]*[*][*]*[/]
/* $$ Approximately, [/][*]<any_char>*[*][/] */
/* $$ Correct regexp ends at FIRST [*][/] */
/* $$ BEWARE: lex will end at the LAST [*][/] in the whole file if you let it. */

/* $$ <single_quoted> and <distinct_object> must contain only "printing ASCII", */
/* $$ which includes space, and consists of ASCII codes 40-126 decimal. */

<single_quoted>        ::- [']([^\\']|[\\][']|[\\][\\])*[']
/* $$ ['].*['] would be OK except for escape sequences. */
/* $$ \ is used as the escape character for ' and \ in <single_quoted>; */
/* $$ i.e., \' is  backslash-quote and \\ is backslash-backslash. */
/* $$ For input and output in TPTP syntax the \ is present. */

/*----All <distinct_object>s are implicitly unequal; ~("a" = "b").  */
<distinct_object>      ::- ["]([^\\"]|[\\]["]|[\\][\\])*["]
/* $$ ["].*["] would be OK except for escape sequences. */
/* $$ \ is used as the escape character for " and \ in <distinct_object>; */
/* $$ i.e., \" is  backslash-quote and \\ is backslash-backslash. */
/* $$ For input and output in TPTP syntax the \ is present. */


/*------------------------------------------------------------------------------ */
/*----If you are writing a new TPTP parser, you do not need to parse the old */
/*----style TPTP formulae, i.e., ignore from here on down. */
/*----Old style TPTP clauses and formulae */
<TPTP_FOF_file>      ::= <TPTP_FOF_input>*
<TPTP_FOF_input>     ::= <TPTP_input_formula> | <include> | <comment>
<TPTP_input_formula> ::= input_formula(<name>,<formula_role>,<hof_formula>).
<TPTP_CNF_file>      ::= <TPTP_CNF_input>*
<TPTP_CNF_input>     ::= <TPTP_input_clause> | <include> | <comment>
<TPTP_input_clause>  ::= input_clause(<name>,<formula_role>,<TPTP_literals>).

/*----Old style TPTP CNF */
<TPTP_literals>      ::= [] | [<TPTP_literal><rest_TPTP_literals>*]
<rest_TPTP_literals> ::= ,<TPTP_literal>
<TPTP_literal>       ::= <TPTP_sign><atomic_formula>
<TPTP_sign>          ::= ++ | --

